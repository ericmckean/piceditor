<languageVersion: 1.0;>
kernel RoundPixel
<   namespace : "be.neuroproductions";
    vendor : "Neuro Productions Comm. V.";
    version : 1;
    description : "Round pixel filter";
>
{
 
    parameter float dist
    <
        minValue: float(1);
        maxValue: float(300);
        defaultValue: float(100);
        description: "distance";
    >;
 
 parameter float size
    <
        minValue: float(0.0);
        maxValue: float(2.0);
        defaultValue: float(1.0);
        description: "size";
    >;
 parameter float edgeAlpha
    <
        minValue: float(0.0);
        maxValue: float(300.0);
        defaultValue: float(2.0);
        description: "edgeAlpha";
    >;
 
    input image4 src;
    output pixel4 dst;
 
    void
    evaluatePixel()
    {
        float2 inP= outCoord();
        float xPos = (floor((inP.x)/dist)*dist);
        float yPos = (floor((inP.y)/dist)*dist);
 
        float difX = inP.x -xPos;
        float difY = inP.y -yPos;
 
        float2 newP;
        newP.x =xPos;
        newP.y = yPos;
 
        float distt = distance(inP-(dist/2.0),newP);
 
        dst = sampleNearest(src,newP);
 
        if(dst.a != 0.0)
        {
            if (2.0* distt/size< dist) // set pixels outside the circle on alpha =0
            {
                dst.a =0.0;
            }
            else if (2.0* (distt)/(size)<dist-edgeAlpha) // create a soft edge
            {
                dst.a=( dist-( 2.0* (distt)/(size)))/edgeAlpha ;
            }
        }
 
    }
}